plugins {
	id 'org.springframework.boot' version '2.3.3.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
}

group = 'com.trackthatbug'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "Hoxton.SR8")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-devtools'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.security:spring-security-test'
}

// OS Detection from https://stackoverflow.com/a/31443955
import org.apache.tools.ant.taskdefs.condition.Os
task detectOs {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        project.ext.set('OS', 'Windows')
        project.ext.set('cmdExt', '.cmd')
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        project.ext.set('OS', 'Mac')
        project.ext.set('cmdExt', '')
    } else if (Os.isFamily(Os.FAMILY_UNIX)) {
        // NB: Mac also matches Unix, so test for Mac first
        project.ext.set('OS', 'Unix')
        project.ext.set('cmdExt', '')
    }
    println "OS Detected: ${project.ext.OS}"
}

task webpack(dependsOn: detectOs, type: Exec) {
	inputs.file("package-lock.json").withPathSensitivity(PathSensitivity.RELATIVE)
	inputs.dir("src").withPathSensitivity(PathSensitivity.RELATIVE)
	outputs.dir("$buildDir/resources/main/static/js")
	outputs.cacheIf { true }
	commandLine "$projectDir/node_modules/.bin/webpack${project.ext.cmdExt}"
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
	useJUnitPlatform()
}

build.dependsOn webpack
